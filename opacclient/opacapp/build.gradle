apply plugin: 'com.android.library'
apply plugin: 'jacoco'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 217
        versionName "6.2.6"
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        // disable 'AndroidLintRtlHardcoded' // Throws an "Unknown issue id" for me
        disable 'MissingTranslation'
        disable 'UnusedAttribute'
        warning 'InvalidPackage'
    }

    flavorDimensions "dependencies"
    productFlavors {
        googlePlayServices {
            dimension "dependencies"
        }
        foss {
            dimension "dependencies"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding 'UTF-8'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

    useLibrary 'org.apache.http.legacy'
    useLibrary 'android.test.mock'
    publishNonDefault true

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    api(project(':libopac')) {
        transitive = false
    }
    api "org.jetbrains.kotlin:kotlin-stdlib:1.3.11"
    api 'io.sentry:sentry-android:1.7.16'
    api 'org.jsoup:jsoup:1.8.3'
    api 'com.github.opacapp:material-components-android:d290745a71'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.percentlayout:percentlayout:1.0.0'
    api 'androidx.appcompat:appcompat:1.0.2'
    api 'androidx.palette:palette:1.0.0'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.preference:preference:1.0.0'
    api 'org.apmem.tools:layouts:1.10@aar'
    api 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    api 'joda-time:joda-time:2.9.9'
    api 'org.joda:joda-convert:1.8'
    api 'su.j2e:rv-joiner:1.0.6'
    api 'com.squareup.okhttp3:okhttp:3.12.1'
    api 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'
    api 'com.squareup.retrofit2:retrofit:2.3.0'
    api 'com.squareup.retrofit2:converter-moshi:2.1.0'
    api 'com.samskivert:jmustache:1.13'
    api 'com.github.johan12345:material-intro:a7791652d9'
    api 'com.github.bumptech.glide:glide:3.8.0'
    api 'com.github.bumptech.glide:okhttp3-integration:1.5.0@aar'
    api 'androidx.work:work-runtime:2.2.0'
    api 'net.sourceforge.streamsupport:streamsupport-cfuture:1.6.0'
    api 'androidx.multidex:multidex:2.0.0'
    debugApi 'com.facebook.stetho:stetho:1.5.0'
    debugApi 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    debugApi 'com.squareup.leakcanary:leakcanary-android:2.1'
    testApi 'org.mockito:mockito-core:3.2.0'
    testApi 'junit:junit:4.12'
    testApi 'com.squareup.okhttp3:okhttp:3.12.1'
    testApi 'com.squareup.retrofit2:retrofit-mock:2.3.0'
    testApi 'commons-io:commons-io:2.5'
    googlePlayServicesApi 'com.google.android.gms:play-services-safetynet:15.0.1'
    // We don't want to rely on the CommonsWare Maven repo, so we include these libraries as JARs
    api files('libs/adapter-1.0.1.jar')
    api files('libs/endless-1.2.3.jar')
}

jacoco {
    toolVersion = "0.7.4.201502262128"
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testGooglePlayServicesDebugUnitTest') {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    jacocoClasspath = configurations['jacocoAnt']

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/googlePlayServicesDebug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files("${buildDir}/jacoco/testGooglePlayServicesDebugUnitTest.exec")
}
