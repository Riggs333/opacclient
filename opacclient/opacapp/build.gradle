apply plugin: 'com.android.library'
apply plugin: 'jacoco'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 186
        versionName "5.3.1"
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        // disable 'AndroidLintRtlHardcoded' // Throws an "Unknown issue id" for me
        disable 'MissingTranslation'
        disable 'UnusedAttribute'
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding 'UTF-8'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

    useLibrary 'org.apache.http.legacy'
    publishNonDefault true

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    api(project(':libopac')) {
        transitive = false
    }
    api('ch.acra:acra:4.9.0') {
        exclude group: 'org.json', module: 'json'
    }
    api 'org.jsoup:jsoup:1.8.3'
    api 'com.android.support:design:27.1.0'
    api 'com.android.support:support-v4:27.1.0'
    api 'com.android.support:percent:27.1.0'
    api 'com.android.support:appcompat-v7:27.1.0'
    api 'com.android.support:palette-v7:27.1.0'
    api 'com.android.support:cardview-v7:27.1.0'
    api 'com.android.support:preference-v7:27.1.0'
    api 'org.apmem.tools:layouts:1.8@aar'
    api 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    api 'net.opacapp:multiline-collapsingtoolbar:27.1.0'
    api 'joda-time:joda-time:2.8.2'
    api 'org.joda:joda-convert:1.8'
    api 'su.j2e:rv-joiner:1.0.6'
    api 'joda-time:joda-time:2.8.2'
    api 'com.squareup.okhttp3:okhttp:3.10.0'
    api "com.squareup.okhttp3:okhttp-urlconnection:3.9.0"
    api 'com.squareup.retrofit2:retrofit:2.2.0'
    api 'com.squareup.retrofit2:converter-moshi:2.1.0'
    api 'com.samskivert:jmustache:1.13'
    api 'com.github.johan12345:material-intro:custom_layout-1.5.5-g47ed124'
    api 'com.github.bumptech.glide:glide:3.8.0'
    api 'com.github.bumptech.glide:okhttp3-integration:1.5.0@aar'
    api 'com.evernote:android-job:1.2.0'
    api 'net.sourceforge.streamsupport:streamsupport-cfuture:1.6.0'
    debugApi 'com.facebook.stetho:stetho:1.5.0'
    debugApi 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    debugApi 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.okhttp3:okhttp:3.10.0'
    testImplementation 'com.squareup.retrofit2:retrofit-mock:2.3.0'
    testImplementation 'commons-io:commons-io:2.5'

    // We don't want to rely on the CommonsWare Maven repo, so we include these libraries as JARs
    api files('libs/adapter-1.0.1.jar')
    api files('libs/endless-1.2.3.jar')
}

jacoco {
    toolVersion = "0.7.4.201502262128"
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    jacocoClasspath = configurations['jacocoAnt']

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")
}
